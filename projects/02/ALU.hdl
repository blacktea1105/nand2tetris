// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zx
    Mux16(a=x, b[0..15]=false, sel=zx, out=tempXInZX);

    // nx
    Not16(in=tempXInZX, out=notXInNX);
    Mux16(a=tempXInZX, b=notXInNX, sel=nx, out=tempXInNX);

    // zy
    Mux16(a=y, b[0..15]=false, sel=zy, out=tempYInZY);

    // ny
    Not16(in=tempYInZY, out=notYInNY);
    Mux16(a=tempYInZY, b=notYInNY, sel=ny, out=tempYInNY);

    // f
    Add16(a=tempXInNX, b=tempYInNY, out=addXY);
    And16(a=tempXInNX, b=tempYInNY, out=andXY);
    Mux16(a=andXY, b=addXY, sel=f, out=fOut);

    // no
    Not16(in=fOut, out=notFOut);
    Mux16(a=fOut, b=notFOut, sel=no, out=out, out[15]=ngFinalOut, out[0..7]=lowFinalOut, out[8..15]=highFinalOut);

    // zr
    Or8Way(in=lowFinalOut, out=outLowOr8Way);
    Or8Way(in=highFinalOut, out=outHighOr8Way);
    Or(a=outLowOr8Way, b=outHighOr8Way, out=outputIsNotZero);
    Mux(a=true, b=false, sel=outputIsNotZero, out=zr);

    // ng
    And(a=ngFinalOut, b=true, out=outputIsNg);
    Mux(a=false, b=true, sel=outputIsNg, out=ng);
}